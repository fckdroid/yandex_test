import groovy.json.JsonSlurper

plugins {
    id "me.tatarka.retrolambda" version "3.5.0"
}
apply plugin: "com.android.application"
apply plugin: "com.frogermcs.androiddevmetrics"
apply plugin: 'org.greenrobot.greendao'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "rxlll.yandextest"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:2.0.1'
    }
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
    productFlavors {
        dev {
            //TODO: Provide your own api_key`s to "~./app/yandex-api.json"
            buildConfigField('String',
                    'TRANSLATOR_API_URL',
                    "\"" + (new JsonSlurper().parseText(file('yandex-api.json').text)).translator_api_url + "\"")
            buildConfigField('String',
                    'TRANSLATOR_API_KEY',
                    "\"" + (new JsonSlurper().parseText(file('yandex-api.json').text)).translator_api_key + "\"")
            buildConfigField('String',
                    'DICTIONARY_API_URL',
                    "\"" + (new JsonSlurper().parseText(file('yandex-api.json').text)).dictionary_api_url + "\"")
            buildConfigField('String',
                    'DICTIONARY_API_KEY',
                    "\"" + (new JsonSlurper().parseText(file('yandex-api.json').text)).dictionary_api_key + "\"")
        }
    }
}

dependencies {
    /*The definitions of the <rootProject.*> are placed in ~.dependencies.gradle*/
    annotationProcessor rootProject.moxyCompiler
    annotationProcessor rootProject.daggerCompiler
    compile(
            /*core*/
            rootProject.rxJava,
            rootProject.rxAndroid,
            rootProject.rxPermissions,
            rootProject.rxJavaAdapter,
            /*MVP*/
            rootProject.moxy,
            rootProject.moxyAndroid,
            rootProject.moxyCompiler,
            rootProject.moxyAppcompat,
            /*dependency injection*/
            rootProject.dagger,
            /*network*/
            rootProject.retrofit,
            rootProject.retrofitGsonConverter,
            /*view*/
            rootProject.conductor,
            rootProject.conductorSupport,
            rootProject.conductorRxLifecycle,
            /*database*/
            rootProject.greendao,
            /*design*/
            rootProject.glide,
            rootProject.design,
            rootProject.support,
            rootProject.appcompat,
            rootProject.constraint,
            rootProject.recyclerview,
    )
    debugCompile(
            rootProject.stetho,
            rootProject.stethoOkhttp,
            rootProject.leakcanary
    )
    testCompile rootProject.junit
    androidTestCompile(rootProject.espresso, {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

